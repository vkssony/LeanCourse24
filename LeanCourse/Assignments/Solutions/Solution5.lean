import LeanCourse.Common
import Mathlib.Data.Complex.Exponential
noncomputable section
open Real Function Nat BigOperators Set Finset
open Classical


/-

* From Mathematics in Lean https://leanprover-community.github.io/mathematics_in_lean
  Read chapters 5 (mostly section 2) and 6 (mostly sections 1 and 2).

* Do the exercises corresponding to these sections in the `LeanCourse/MIL` folder.
  There are solutions in the solution folder in case you get stuck.

* Hand in the solutions to the exercises under "Exercises to hand-in". Deadline: 12.11.

* Make sure the file you hand-in compiles without error.
  Use `sorry` if you get stuck on an exercise.
-/


/-! # Exercises to practice. -/

/- A note on definitions -/

lemma my_lemma : 3 + 1 = 4 := rfl
def myThree : ‚Ñï := 3

/-
Tactics like `simp` and `rw` will not unfold definitions unless instructed to.
Tactics like `exact` and `apply` will unfold definitions if needed.
Uncomment the following lines one-by-one to see the difference. -/

example : myThree + 1 = 4 := by
  -- rw [my_lemma] -- fails
  -- simp [my_lemma] -- fails to use `my_lemma`
  exact my_lemma -- works
  -- apply my_lemma -- works
  -- rw [myThree, my_lemma] -- works after instructing `rw` to unfold the definition
  -- simp [myThree] -- works after instructing `simp` to unfold the definition
                    -- (it doesn't need `my_lemma` to then prove the goal)
  --sorry


/- The following exercises are to practice with casts. -/
example (n : ‚Ñ§) (h : (n : ‚Ñö) = 3) : 3 = n := by {
  norm_cast at h
  rw [h]
  }

example (n m : ‚Ñï) (h : (n : ‚Ñö) + 3 ‚â§ 2 * m) : (n : ‚Ñù) + 1 < 2 * m := by {
  norm_cast at h ‚ä¢
  calc
    n + 1 < n + 3 := by omega
    _ ‚â§ 2 * m := h
  }

example (n m : ‚Ñï) (h : (n : ‚Ñö) = m ^ 2 - 2 * m) : (n : ‚Ñù) + 1 = (m - 1) ^ 2 := by {
  have : (n : ‚Ñù) = m ^ 2 - 2 * m := by norm_cast at h ‚ä¢
  rw [this]
  ring
  }

example (n m : ‚Ñï) : (n : ‚Ñù) < (m : ‚Ñù) ‚Üî n < m := by {
  norm_cast -- this also works: `exact cast_lt`
  }

example (n m : ‚Ñï) (hn : 2 ‚à£ n) (h : n / 2 = m) : (n : ‚Ñö) / 2 = m := by {
  norm_cast
  }

example (q q' : ‚Ñö) (h : q ‚â§ q') : exp q ‚â§ exp q' := by {
  gcongr
  norm_cast
  }

example (n : ‚Ñ§) (h : 0 < n) : 0 < Real.sqrt n := by {
  have : 0 < (n : ‚Ñù) := by norm_cast
  exact sqrt_pos_of_pos this -- via `exact?`
  }

/- Working with `Finset` is very similar to working with `Set`.
However, some notation, such as `f '' s` or `ùí´ s` doesn't exist for `Finset`. -/
example (s t : Finset ‚Ñï) : (s ‚à™ t) ‚à© t = (s ‚à© t) ‚à™ t := by {
  ext
  simp
  }

example {Œ± Œ≤ : Type*} (f : Œ± ‚Üí Œ≤) (s : Finset Œ±) (y : Œ≤) : y ‚àà s.image f ‚Üî ‚àÉ x ‚àà s, f x = y := by {
  simp
  }

/- `Disjoint` can be used to state that two (fin)sets are disjoint.
Use `Finset.disjoint_left` (or `Set.disjoint_left`) to unfold its definition.
If you have `x ‚àà t \ s` apply `simp` first to get a conjunction of two conditions.
-/
example {Œ± : Type*} (s t : Finset Œ±) : Disjoint s (t \ s) := by {
  rw [Finset.disjoint_left]
  intro x hxs hxt
  simp at hxt
  tauto
  }


/- Let's define the Fibonacci sequence -/
def fibonacci : ‚Ñï ‚Üí ‚Ñï
  | 0 => 0
  | 1 => 1
  | (n + 2) => fibonacci (n + 1) + fibonacci n

/- Prove the following exercises by induction. -/

example (n : ‚Ñï) : ‚àë i in range n, fibonacci (2 * i + 1) = fibonacci (2 * n) := by {
  induction n with
  | zero => simp [fibonacci]
  | succ n ih =>
    simp [sum_range_succ, ih, fibonacci]
    rw [add_comm]
  }

example (n : ‚Ñï) : ‚àë i in range n, (fibonacci i : ‚Ñ§) = fibonacci (n + 1) - 1 := by {
  induction n with
  | zero => simp [fibonacci]
  | succ n ih =>
    simp [sum_range_succ, ih, fibonacci] -- you can also `rw` and then `push_cast`
    ring
  }

example (n : ‚Ñï) : 6 * ‚àë i in range (n + 1), i ^ 2 = n * (n + 1) * (2 * n + 1) := by {
  induction n with
  | zero => simp
  | succ n ih =>
    simp [sum_range_succ _ (n + 1), mul_add 6, ih]
    ring
  }

def fac : ‚Ñï ‚Üí ‚Ñï
  | 0       => 1
  | (n + 1) => (n + 1) * fac n

theorem pow_two_le_fac (n : ‚Ñï) : 2 ^ n ‚â§ fac (n + 1) := by {
  induction n with
  | zero => norm_num [fac]
  | succ n ih =>
    rw [fac, pow_add, mul_comm]
    gcongr
    omega
  }

example (n : ‚Ñï) : fac n = ‚àè i in range n, (i + 1) := by {
  induction n with
  | zero => simp [fac]
  | succ k ih =>
    rw [fac, prod_range_succ, ‚Üê ih, mul_comm]
  }

example (n : ‚Ñï) : fac (2 * n) = fac n * 2 ^ n * ‚àè i in range n, (2 * i + 1) := by {
  induction n with
  | zero => simp [fac]
  | succ n ih =>
    simp [fac, ih, prod_range_succ]
    ring
  }





/- **Exercise**.
Define scalar multiplication of a real number and a `Point`. -/

@[ext] structure Point where
  x : ‚Ñù
  y : ‚Ñù
  z : ‚Ñù

-- give definition here


/- **Exercise**.Define Pythagorean triples, i.e. triples `a b c : ‚Ñï` with `a^2 + b^2 = c^2`.
Give an example of a Pythagorean triple, and show that multiplying a Pythagorean triple by a
constant gives another Pythagorean triple. -/

-- give definition here



/- Prove that triples of equivalent types are equivalent. -/

@[ext] structure Triple (Œ± : Type*) where
  x : Œ±
  y : Œ±
  z : Œ±

example (Œ± Œ≤ : Type*) (e : Œ± ‚âÉ Œ≤) : Triple Œ± ‚âÉ Triple Œ≤ where
  toFun := fun ‚ü®x,y,z‚ü© ‚Ü¶ ‚ü®e x, e y, e z‚ü©
  invFun := fun ‚ü®x,y,z‚ü© ‚Ü¶ ‚ü®e.invFun x, e.invFun y, e.invFun z‚ü©
  left_inv := by simp [LeftInverse]
  right_inv := by simp [Function.RightInverse, LeftInverse]


/- 5. Show that if `G` is an abelian group then triples from elements of `G` is an abelian group. -/

class AbelianGroup' (G : Type*) where
  add (x : G) (y : G) : G
  comm (x y : G) : add x y = add y x
  assoc (x y z : G) : add (add x y) z = add x (add y z)
  zero : G
  add_zero : ‚àÄ x : G, add x zero = x
  neg : G ‚Üí G
  add_neg : ‚àÄ x : G, add x (neg x) = zero

def Triple.add {Œ± : Type*} [AbelianGroup' Œ±] (a b : Triple Œ±) : Triple Œ± where
  x := AbelianGroup'.add a.x b.x
  y := AbelianGroup'.add a.y b.y
  z := AbelianGroup'.add a.z b.z

example (G : Type*) [AbelianGroup' G] : AbelianGroup' (Triple G) where
  add := Triple.add
  comm := by simp [Triple.add, AbelianGroup'.comm]
  assoc := by simp [Triple.add, AbelianGroup'.assoc]
  zero := ‚ü®AbelianGroup'.zero, AbelianGroup'.zero, AbelianGroup'.zero‚ü©
  add_zero := by simp [Triple.add, AbelianGroup'.add_zero]
  neg a := ‚ü®AbelianGroup'.neg a.x, AbelianGroup'.neg a.y, AbelianGroup'.neg a.z‚ü©
  add_neg := by simp [Triple.add, AbelianGroup'.add_neg]


/-! # Exercises to hand-in. -/

/- **Exercise**.
Define the structure of "strict bipointed types", i.e. a type together with 2 unequal points
`x‚ÇÄ ‚â† x‚ÇÅ` in it.
Then state and prove the lemma that for any element of a strict bipointed type we have
`‚àÄ z, z ‚â† x‚ÇÄ ‚à® z ‚â† x‚ÇÅ.` -/

-- give the definition here
structure StrictBipointed (Œ± : Type) :=
  (here there : Œ±)
  (strict : here ‚â† there)

-- state and prove the lemma here
lemma StrictBipointed.ne_or_ne {Œ± : Type} (X : StrictBipointed Œ±) (x : Œ±) :
    x ‚â† X.here ‚à® x ‚â† X.there := by
  by_cases hx : x = X.here
  ¬∑ subst hx
    right
    exact X.strict
  ¬∑ left
    exact hx


/- Prove by induction that `‚àë_{i = 0}^{n} i^3 = (‚àë_{i=0}^{n} i) ^ 2`. -/
open Finset in
lemma sum_cube_eq_sq_sum (n : ‚Ñï) :
    (‚àë i in range (n + 1), (i : ‚Ñö) ^ 3) = (‚àë i in range (n + 1), (i : ‚Ñö)) ^ 2 := by {
  have : ‚àÄ n, (‚àë i in range (n + 1), (i : ‚Ñö)) = n * (n + 1) / 2
  ¬∑ intro n; induction n with
    | zero => simp
    | succ n ih =>
      rw [sum_range_succ, ih]
      push_cast
      ring
  rw [this]
  induction n with
  | zero => simp
  | succ n ih =>
    rw [sum_range_succ, ih]
    push_cast
    ring
  }

/-
Suppose `(A i)_i` is a sequence of sets indexed by a well-ordered type
(this is an order where every nonempty set has a minimum element).
We now define a new sequence by `C n = A n \ (‚ãÉ i < n, A i)`.
In this exercise, we show that the new sequence is pairwise disjoint but has the same union as the
original sequence.
-/
lemma disjoint_unions {Œπ Œ± : Type*} [LinearOrder Œπ] [wf : WellFoundedLT Œπ] (A C : Œπ ‚Üí Set Œ±)
  (hC : ‚àÄ i, C i = A i \ ‚ãÉ j < i, A j) : Pairwise (Disjoint on C) ‚àß ‚ãÉ i, C i = ‚ãÉ i, A i := by {
  have h := wf.wf.has_min -- this hypothesis allows you to use well-orderedness
  have lt_disjoint : ‚àÄ i j, i < j ‚Üí Disjoint (C i) (C j)
  ¬∑ intro i j hij
    simp [Set.disjoint_left, hC]
    tauto
  constructor
  ¬∑ intro i j hij
    obtain h|h := hij.lt_or_lt
    ¬∑ exact lt_disjoint i j h
    ¬∑ exact (lt_disjoint j i h).symm
  ¬∑ apply subset_antisymm
    ¬∑ gcongr with i
      rw [hC]
      exact diff_subset
    ¬∑ simp [Set.subset_def, hC]
      intros x i hx
      obtain ‚ü®j, imem, hj‚ü© := h { i | x ‚àà A i } ‚ü®i, hx‚ü©
      use j
      tauto
  }

#check WellFoundedLT.induction
-- Alternate proof using wellfounded induction for the last part
lemma disjoint_unions' {Œπ Œ± : Type*} [LinearOrder Œπ] [wf : WellFoundedLT Œπ] (A C : Œπ ‚Üí Set Œ±)
  (hC : ‚àÄ i, C i = A i \ ‚ãÉ j < i, A j) : Pairwise (Disjoint on C) ‚àß ‚ãÉ i, C i = ‚ãÉ i, A i := by {

  have lt_disjoint : ‚àÄ i j, i < j ‚Üí Disjoint (C i) (C j)
  ¬∑ intro i j hij
    simp [Set.disjoint_left, hC]
    tauto

  constructor
  ¬∑ intro i j hij
    obtain h|h := hij.lt_or_lt
    ¬∑ exact lt_disjoint i j h
    ¬∑ exact Disjoint.symm (lt_disjoint j i h)

  ¬∑ apply subset_antisymm
    ¬∑ gcongr with i
      rw [hC]
      exact diff_subset -- via `exact?`
    ¬∑ simp [Set.subset_def]
      intro x i xmem
      induction i using wf.induction with | _ i ih =>
      by_cases hx : ‚àÉ j < i, x ‚àà A j
      ¬∑ obtain ‚ü®j, h1, h2‚ü© := hx
        exact ih j h1 h2
      ¬∑ simp [hC] at hx ‚ä¢
        use i
  }


/- Next, we'll prove that if `2 ^ n - 1` is prime, then `n` is prime.
The first exercise is a preparation, and I've given you a skeleton of the proof for the
second exercise. Note how we do some computations in the integers, since the subtraction on `‚Ñï`
is less well-behaved.
(The converse is not true, because `89 ‚à£ 2 ^ 11 - 1`) -/

lemma not_prime_iff (n : ‚Ñï) :
    ¬¨ Nat.Prime n ‚Üî n = 0 ‚à® n = 1 ‚à® ‚àÉ a b : ‚Ñï, 2 ‚â§ a ‚àß 2 ‚â§ b ‚àß n = a * b := by {
  rw [Nat.prime_def_lt']
  constructor
  ¬∑ push_neg
    intro h
    rw [‚Üê or_assoc]
    by_cases h2 : n = 0 ‚à® n = 1
    ¬∑ left
      trivial
    ¬∑ right
      have : 2 < n := by omega
      obtain ‚ü®a, ha, han, hadvd‚ü© := h (by linarith)
      obtain ‚ü®b, hb‚ü© := hadvd
      have : 2 ‚â§ b := by nlinarith
      use a, b
  ¬∑ rintro (hn0|hn1|hn) h
    ¬∑ linarith
    ¬∑ linarith
    ¬∑ obtain ‚ü®a, b, ha, hb, hab‚ü© := hn
      have : a < n := by nlinarith
      have : a ‚à£ n := Dvd.intro b (hab.symm) -- via `exact?`
      tauto
  }

-- Here's an alternate proof through applying congruence rules manually.
lemma not_prime_iff' (n : ‚Ñï) :
    ¬¨ Nat.Prime n ‚Üî n = 0 ‚à® n = 1 ‚à® ‚àÉ a b : ‚Ñï, 2 ‚â§ a ‚àß 2 ‚â§ b ‚àß n = a * b := by {
  rw [‚Üê or_assoc, ‚Üê Nat.le_one_iff_eq_zero_or_eq_one, ‚Üê not_lt, ‚Üê imp_iff_not_or, Nat.prime_def_lt',
    succ_le]
  push_neg
  simp
  apply imp_congr .rfl
  apply exists_congr
  intro k
  apply and_congr_right
  intro hk
  rw [dvd_def, ‚Üê exists_and_left]
  apply exists_congr
  intro l
  apply and_congr_left
  intro h
  subst h
  rw [succ_le]
  apply lt_mul_iff_one_lt_right
  linarith
  }

lemma prime_of_prime_two_pow_sub_one (n : ‚Ñï) (hn : Nat.Prime (2 ^ n - 1)) : Nat.Prime n := by {
  by_contra h2n
  rw [not_prime_iff] at h2n
  obtain rfl|rfl|‚ü®a, b, ha, hb, rfl‚ü© := h2n
  ¬∑ simp [not_prime_zero] at hn
  ¬∑ norm_num at hn
  have h : (2 : ‚Ñ§) ^ a - 1 ‚à£ (2 : ‚Ñ§) ^ (a * b) - 1
  ¬∑ rw [‚Üê Int.modEq_zero_iff_dvd]
    calc (2 : ‚Ñ§) ^ (a * b) - 1
        ‚â° ((2 : ‚Ñ§) ^ a) ^ b - 1 [ZMOD (2 : ‚Ñ§) ^ a - 1] := by rw [pow_mul] -- via `rw?`
      _ ‚â° (1 : ‚Ñ§) ^ b - 1 [ZMOD (2 : ‚Ñ§) ^ a - 1] := by gcongr; exact Int.modEq_sub (2 ^ a) 1 -- via `exact?`
      _ ‚â° 0 [ZMOD (2 : ‚Ñ§) ^ a - 1] := by simp
  have h2 : 2 ^ 2 ‚â§ 2 ^ a := by gcongr; norm_num
  have h3 : 1 ‚â§ 2 ^ a := by linarith
  have h4 : 2 ^ a - 1 ‚â† 1 := by zify; simp [h3]; linarith
  have h5 : 2 ^ a - 1 < 2 ^ (a * b) - 1 := by
    apply tsub_lt_tsub_right_of_le h3
    gcongr
    ¬∑ norm_num
    ¬∑ nlinarith
  have h6' : 2 ^ 0 ‚â§ 2 ^ (a * b) := by gcongr; simp; simp
  have h6 : 1 ‚â§ 2 ^ (a * b) := h6'
  have h' : 2 ^ a - 1 ‚à£ 2 ^ (a * b) - 1 := by norm_cast at h
  rw [Nat.prime_def_lt] at hn
  apply h4
  apply hn.2
  ¬∑ exact h5
  ¬∑ exact h'
  }

/- Prove that for positive `a` and `b`, `a^2 + b` and `b^2 + a` cannot both be squares.
Prove it on paper first! -/
lemma not_isSquare_sq_add_or (a b : ‚Ñï) (ha : 0 < a) (hb : 0 < b) :
    ¬¨ IsSquare (a ^ 2 + b) ‚à® ¬¨ IsSquare (b ^ 2 + a) := by {
  have : ‚àÄ {a b : ‚Ñï}, 0 < a ‚Üí a ‚â§ b ‚Üí ¬¨ IsSquare (b ^ 2 + a)
  ¬∑ intro a b ha hab ‚ü®c, hc‚ü©
    have : b < c := by nlinarith
    have : c < b + 1 := by nlinarith
    linarith
  obtain h|h := le_total a b
  ¬∑ exact .inr (this ha h)
  ¬∑ exact .inl (this hb h)
  }


/-- Let's prove that the positive reals form a group under multiplication.
Note: in this exercise `rw` and `simp` will not be that helpful, since the definition is hidden
behind notation. But you can use apply to use the lemmas about real numbers. -/

abbrev PosReal : Type := {x : ‚Ñù // 0 < x}

def groupPosReal : Group PosReal where
  mul x y := ‚ü®x.1 * y.1, mul_pos x.2 y.2‚ü©
  mul_assoc x y z := mul_assoc x y z
  one := ‚ü®1, by norm_num‚ü©
  one_mul x := one_mul x
  mul_one x := mul_one x
  inv x := ‚ü®x.1‚Åª¬π, inv_pos.mpr x.2‚ü©
  inv_mul_cancel x := by ext; apply inv_mul_cancel‚ÇÄ; exact x.2.ne'



/-
Compute by induction the cardinality of the powerset of a finite set.

Hints:
* Use `Finset.induction` as the induction principle, using the following pattern:
```
  induction s using Finset.induction with
  | empty => sorry
  | @insert x s hxs ih => sorry
```
* You will need various lemmas about the powerset, search them using Loogle.
  The following queries will be useful for the search:
  `Finset.powerset, insert _ _`
  `Finset.card, Finset.image`
  `Finset.card, insert _ _`
* As part of the proof, you will need to prove an injectivity condition
  and a disjointness condition.
  Separate these out as separate lemmas or state them using `have` to break up the proof.
* Mathlib already has `card_powerset` as a simp-lemma, so we remove it from the simp-set,
  so that you don't actually simplify with that lemma.
-/
attribute [-simp] Finset.card_powerset
attribute [-simp] Multiset.card_powerset
#check Finset.induction

example (Œ± : Type*) (s : Finset Œ±) : Finset.card (powerset s) = 2 ^ Finset.card s := by {
  induction s using Finset.induction with
  | empty => simp
  | @insert x s hxs ih =>
      rw [Finset.powerset_insert, Finset.card_union_of_disjoint, Finset.card_image_of_injOn,
        Finset.card_insert_of_not_mem hxs]
      ¬∑ omega
      ¬∑ intro t ht t' ht' htt'
        have or_congr_right_iff (p q r : Prop) : (p ‚à® q ‚Üî p ‚à® r) ‚Üî ((p ‚Üí ¬¨ q ‚àß ¬¨ r) ‚Üí (q ‚Üî r)) := by
          tauto
        simp [Finset.ext_iff, or_congr_right_iff] at htt'
        ext y
        apply htt'
        rintro rfl
        simp at ht ht'
        constructor
        ¬∑ exact fun hy ‚Ü¶ hxs (ht hy)
        ¬∑ exact fun hy ‚Ü¶ hxs (ht' hy)
      ¬∑ rw [Finset.disjoint_left]
        intro t ht h2t
        simp at ht h2t
        obtain ‚ü®t, h2t, rfl‚ü© := h2t
        apply hxs
        apply ht
        exact mem_insert_self x t
  }
